// HTML Un-Grammar.
//
// This grammar specifies the structure of Rust's concrete syntax tree.
// It does not specify parsing rules (ambiguities, precedence, etc are out of scope).
// Tokens are processed -- contextual keywords are recognised, compound operators glued.
//
// Legend:
//
//   //          				-- comment
//   Name =      				-- non-terminal definition
//   'ident'     				-- token (terminal)
//   A B         				-- sequence
//   A | B       				-- alternation
//   A*          				-- zero or more repetition
//   (A (',' A)* ','?)	-- repetition of node A separated by ',' and allowing a trailing comma
//   (A (',' A)*)	      -- repetition of node A separated by ',' without a trailing comma
//   A?          				-- zero or one repetition
//   (A)         				-- same as A
//   label:A     				-- suggested name for field of AST node

// NOTES
//
// - SyntaxNode, SyntaxToken and SyntaxElement will be stripped from the codegen
// - Bogus nodes are special nodes used to keep track of broken code; they are
//   not part of the grammar but they will appear inside the green tree


///////////////
// BOGUS NODES
///////////////
// SyntaxElement is a generic data structure that is meant to track nodes and tokens
// in cases where we care about both types
//
// As Bogus* node will need to yield both tokens and nodes without discrimination,
// and their children will need to yield nodes and tokens as well.
// For this reason, SyntaxElement = SyntaxElement
SyntaxElement = SyntaxElement

HtmlBogus = SyntaxElement*
HtmlBogusElement = SyntaxElement*
HtmlBogusAttribute = SyntaxElement*
AstroBogusFrontmatter = SyntaxElement*
HtmlBogusTextExpression = SyntaxElement*
SvelteBogusBlock = SyntaxElement*

HtmlRoot =
	bom: 'UNICODE_BOM'?
	frontmatter: AnyAstroFrontmatterElement?
	directive: HtmlDirective?
	html: HtmlElementList
	eof: 'EOF'

// <!DOCTYPE html>
// ^^^^^^^^^^^^^^^
HtmlDirective =
	'<'
	'!'
	doctype: 'doctype'
	html: 'html'?
	quirk: 'html_literal'?
	public_id: 'html_string_literal'?
	system_id: 'html_string_literal'?
	'>'

// ==================================
// Elements (AKA tags)
// ==================================

HtmlElementList = AnyHtmlElement*

AnyHtmlElement =
	HtmlSelfClosingElement
	| HtmlElement
	| AnyHtmlContent
	| HtmlCdataSection
	| HtmlBogusElement

AnyHtmlContent =
	HtmlContent
	| HtmlEmbeddedContent
	| AnyHtmlTextExpression
	| AnyGlimmerExpression

AnyHtmlTextExpression =
	HtmlDoubleTextExpression
	| HtmlSingleTextExpression
	| AnySvelteBlock
	| HtmlBogusTextExpression


// {{ expression }}
// ^^^^^^^^^^^^^^^^
HtmlDoubleTextExpression =
	'{{'
	expression: HtmlTextExpression
	'}}'

// {expression}
// ^^^^^^^^^^^^
HtmlSingleTextExpression =
	'{'
	expression: HtmlTextExpression
	'}'

HtmlTextExpression = 'html_literal'


// <a />
HtmlSelfClosingElement =
	'<'
	name: HtmlTagName
	attributes: HtmlAttributeList
	'/'?
	'>'

HtmlElement =
	opening_element: HtmlOpeningElement
	children: HtmlElementList
	closing_element: HtmlClosingElement


// <a href="">
// ^^        ^
HtmlOpeningElement =
	'<'
	name: HtmlTagName
	attributes: HtmlAttributeList
	'>'

// </a>
HtmlClosingElement =
	'<'
	'/'
	name: HtmlTagName
	'>'

// <![CDATA[example]]>
// Reference: https://html.spec.whatwg.org/multipage/syntax.html#cdata-sections
HtmlCdataSection =
	'<![CDATA['
	content: 'html_literal'
	']]>'

AnyAstroFrontmatterElement =
	AstroFrontmatterElement
	| AstroBogusFrontmatter

AstroFrontmatterElement =
	l_fence: '---'
	content: AstroEmbeddedContent
	r_fence: '---'

AstroEmbeddedContent =
	content: 'html_literal'?

// ==================================
// Attributes
// ==================================

HtmlAttributeList = AnyHtmlAttribute*

AnyHtmlAttribute =
	HtmlAttribute
	| HtmlDoubleTextExpression
	| HtmlSingleTextExpression
	| GlimmerSplattribute
	| HtmlBogusAttribute

// <a href="">
//    ^^^^^^^
HtmlAttribute =
	name: HtmlAttributeName
	initializer: HtmlAttributeInitializerClause?


// <a href="">
//        ^^^
HtmlAttributeInitializerClause =
	'='
	value: AnyHtmlAttributeInitializer


AnyHtmlAttributeInitializer =
	HtmlString
	| HtmlSingleTextExpression



// ==================================
// Svelte
// ==================================

AnySvelteBlock =
	SvelteDebugBlock
	| SvelteBogusBlock

// {@debug}
SvelteDebugBlock =
	'{@'
	'debug'
	bindings: SvelteBindingList
	'}'

SvelteBindingList = (SvelteName (',' SvelteName)*)

SvelteName = 'svelte_ident'

HtmlString = value: 'html_string_literal'
HtmlTagName = value: 'html_literal'
HtmlAttributeName = value: 'html_literal'
HtmlContent = value: 'html_literal'
HtmlEmbeddedContent = value: 'html_literal'

// ==================================
// Glimmer
// ==================================

// Union of all Glimmer expressions
AnyGlimmerExpression =
	GlimmerMustacheExpression
	| GlimmerBlockHelper
	| GlimmerBogusExpression

// Bogus node for error recovery
GlimmerBogusExpression = SyntaxElement*

// Basic mustache: {{path args}}
GlimmerMustacheExpression =
	l_curly2_token: '{{'
	path: GlimmerPath
	arguments: GlimmerArgumentList
	r_curly2_token: '}}'

// Block helper: {{#if}}...{{/if}}
GlimmerBlockHelper =
	opening: GlimmerBlockHelperOpening
	children: HtmlElementList
	closing: GlimmerBlockHelperClosing

GlimmerBlockHelperOpening =
	l_curly2_token: '{{'
	hash_token: '#'
	helper: GlimmerPath
	arguments: GlimmerArgumentList
	block_params: GlimmerBlockParams?
	r_curly2_token: '}}'

GlimmerBlockHelperClosing =
	l_curly2_token: '{{'
	slash_token: '/'
	helper: GlimmerPath
	r_curly2_token: '}}'

// Block params: as |item index|
GlimmerBlockParams =
	as_token: 'ident'
	l_pipe_token: '|'
	params: GlimmerBlockParamList
	r_pipe_token: '|'

GlimmerBlockParamList = GlimmerBlockParam*

GlimmerBlockParam =
	name_token: 'ident'

// Path: this.foo, @arg, helper
GlimmerPath =
	segments: GlimmerPathSegmentList

GlimmerPathSegmentList = GlimmerPathSegment*

GlimmerPathSegment =
	value_token: 'ident'

// Arguments
GlimmerArgumentList = AnyGlimmerArgument*

AnyGlimmerArgument =
	GlimmerPositionalArgument
	| GlimmerNamedArgument

GlimmerPositionalArgument =
	value: AnyGlimmerArgumentValue

GlimmerNamedArgument =
	name_token: 'ident'
	eq_token: '='
	value: AnyGlimmerArgumentValue

AnyGlimmerArgumentValue =
	GlimmerPath
	| GlimmerStringLiteral
	| GlimmerLiteral
	| GlimmerSubexpression

// Subexpression: (helper arg1 arg2)
GlimmerSubexpression =
	l_paren_token: '('
	path: GlimmerPath
	arguments: GlimmerArgumentList
	r_paren_token: ')'

GlimmerStringLiteral =
	value_token: 'html_string_literal'

GlimmerLiteral =
	value_token: 'html_literal'

// Splattribute: ...attributes
GlimmerSplattribute =
	dotdotdot_token: '...'
	attributes_token: 'ident'
