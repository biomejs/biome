// GritQL Un-Grammar.
//
// This grammar specifies the structure of Rust's concrete syntax tree.
// It does not specify parsing rules (ambiguities, precedence, etc are out of scope).
// Tokens are processed -- contextual keywords are recognised, compound operators glued.
//
// Legend:
//
//   //          				-- comment
//   Name =      				-- non-terminal definition
//   'ident'     				-- token (terminal)
//   A B         				-- sequence
//   A | B       				-- alternation
//   A*          				-- zero or more repetition
//   (A (',' A)* ','?)	        -- repetition of node A separated by ',' and allowing a trailing comma
//   (A (',' A)*)	            -- repetition of node A separated by ',' without a trailing comma
//   A?          				-- zero or one repetition
//   (A)         				-- same as A
//   label:A     				-- suggested name for field of AST node

// NOTES
//
// - SyntaxNode, SyntaxToken and SyntaxElement will be stripped from the codegen
// - Bogus nodes are special nodes used to keep track of broken code; they are
//   not part of the grammar but they will appear inside the green tree
//


///////////////
// Bogus NODES
///////////////
// SyntaxElement is a generic data structure that is meant to track nodes and tokens
// in cases where we care about both types
//
// As Bogus* node will need to yield both tokens and nodes without discrimination,
// and their children will need to yield nodes and tokens as well.
// For this reason, SyntaxElement = SyntaxElement
SyntaxElement = SyntaxElement

GritBogus = SyntaxElement*
GritBogusContainer = SyntaxElement*
GritBogusDefinition = SyntaxElement*
GritBogusPattern = SyntaxElement*
GritBogusLiteral = SyntaxElement*
GritBogusNamedArg = SyntaxElement*
GritBogusPredicate = SyntaxElement*

GritRoot =
    bom: 'UNICODE_BOM'?
    version: GritVersion?
    language: GritLanguageDeclaration?
    definitions: GritDefinitionList
    pattern: AnyGritPattern?
    definitions_continued: GritDefinitionList
    eof: 'EOF'

// engine biome(1.0)
// ^^^^^^ ^^^^^^^^^^
GritVersion = 'engine' 'biome' '(' version: GritDoubleLiteral ')'

// language js(typescript, jsx);
// ^^^^^^^^ ^^^^^^^^^^^^^^ ^^^^^
GritLanguageDeclaration =
    'language'
    name: GritLanguageName
    flavor: GritLanguageFlavor?
    ';'?

// language js(typescript, jsx);
//            ^^^^^^^^^^^^ ^^^^
GritLanguageFlavor = '(' flavors: GritLanguageFlavorList ')'
GritLanguageFlavorList = GritLanguageFlavorKind (',' GritLanguageFlavorKind)* ','?
GritLanguageFlavorKind =
    flavor_kind: (
        // JavaScript flavors:
        'typescript' | 'jsx'
    )

////////////
/// PATTERNS
////////////

AnyGritPattern =
    AnyGritLiteral
    | GritPatternNot
    | GritPatternOr
    | GritPatternOrElse
    | GritPatternAny
    | GritPatternAnd
    | GritPatternMaybe
    | GritPatternIfElse
    | GritPatternContains
    | GritPatternIncludes
    | GritPatternAfter
    | GritPatternBefore
    | GritWithin
    | GritBubble
    | GritNodeLike
    | GritMapAccessor
    | GritListAccessor
    | GritDot
    | GritSome
    | GritEvery
    | GritUnderscore
    | GritVariable
    | GritRegexPattern
    | GritPatternAs
    | GritPatternLimit
    | GritAssignmentAsPattern
    | GritPatternAccumulate
    | GritRewrite
    | GritLike
    | GritPatternWhere
    | GritMulOperation
    | GritDivOperation
    | GritModOperation
    | GritAddOperation
    | GritSubOperation
    | GritSequential
    | GritFiles
    | GritBracketedPattern
    | GritBogusPattern

GritBracketedPattern = '(' pattern: AnyGritPattern ')'

AnyGritMaybeCurlyPattern = AnyGritPattern | GritCurlyPattern
GritCurlyPattern = '{' pattern: AnyGritPattern '}'

GritPatternList = AnyGritPattern (',' AnyGritPattern)* ','?

AnyGritContainer =
    GritVariable | GritMapAccessor | GritListAccessor | GritBogusContainer

GritMulOperation =
    left: AnyGritPattern '*' right: AnyGritPattern
GritDivOperation =
    left: AnyGritPattern '/' right: AnyGritPattern
GritModOperation =
    left: AnyGritPattern '%' right: AnyGritPattern
GritAddOperation =
    left: AnyGritPattern '+' right: AnyGritPattern
GritSubOperation =
    left: AnyGritPattern '-' right: AnyGritPattern

GritPatternAs =
    pattern: AnyGritPattern 'as' variable: GritVariable

GritPatternLimit =
    pattern: AnyGritPattern 'limit' limit: GritIntLiteral

// statement, in the engine this is a predicate that always evaluates to true
// This is useful for initializing variables at the root of a pattern definition
GritAssignmentAsPattern =
    container: AnyGritContainer '=' pattern: AnyGritPattern

// Primarily used for initializing variables at the root of a pattern definition
GritPatternAccumulate =
    left: AnyGritPattern '+=' right: AnyGritPattern

GritPatternWhere =
    pattern: AnyGritPattern 'where' side_condition: AnyGritPredicate

AnyGritLiteral =
    GritCodeSnippet
    | GritStringLiteral
    | GritIntLiteral
    | GritDoubleLiteral
    | GritBooleanLiteral
    | GritUndefinedLiteral
    | GritMap
    | GritList
    | GritBogusLiteral

GritPatternNot =
    not: GritNot
    pattern: AnyGritPattern

GritPatternOr = 'or' '{' patterns: GritPatternList '}'

GritPatternOrElse = 'orelse' '{' patterns: GritPatternList '}'

GritPatternAny = 'any' '{' patterns: GritPatternList '}'

GritPatternAnd = 'and' '{' patterns: GritPatternList '}'

GritPatternMaybe = 'maybe' pattern: AnyGritMaybeCurlyPattern

GritPatternAfter = 'after' pattern: AnyGritPattern

GritPatternBefore = 'before' pattern: AnyGritPattern

GritSequential = 'sequential' '{' sequential: GritPatternList '}'

GritFiles = 'multifile' '{' files: GritPatternList '}'

GritPatternContains =
    'contains'
    contains: AnyGritMaybeCurlyPattern
    until_clause: GritPatternContainsUntilClause?

GritPatternContainsUntilClause =
    'until'
    until: AnyGritPattern

GritPatternIncludes = 'includes' includes: AnyGritMaybeCurlyPattern

GritRewrite =
    left: AnyGritPattern
    annotation: GritAnnotation?
    '=>'
    right: AnyGritPattern

// --- conditional pattern ------------
GritPatternIfElse =
    'if'
    '('
    if_predicate: AnyGritPredicate
    ')'
    then_pattern: AnyGritMaybeCurlyPattern
    else_clause: GritPatternElseClause?

GritPatternElseClause =
    'else'
    else_pattern: AnyGritMaybeCurlyPattern
// --- conditional pattern ------------

GritWithin =
    'within'
    pattern: AnyGritMaybeCurlyPattern

// $body <: contains bubble($name) `pattern` => with_its_own($name)
//                         ^^^^^^^
GritBubbleScope = '(' variables: GritVariableList ')'

// $body <: contains bubble($name) `pattern` => with_its_own($name)
//                   ^^^^^^^^^^^^^ ^^^^^^^^^ ^^ ^^^^^^^^^^^^^^^^^^^
GritBubble =
    'bubble'
    variables: GritBubbleScope?
    pattern: AnyGritMaybeCurlyPattern

AnyGritMaybeNamedArg =
    AnyGritPattern
    | GritNamedArg
    | GritBogusNamedArg

// return split($string, separator=`\n`)
//                       ^^^^^^^^^^^^^^
GritNamedArg =
    name: GritName
    '='
    pattern: AnyGritPattern

// return split($string, separator=`\n`)
//              ^^^^^^^^ ^^^^^^^^^^^^^^
GritNamedArgList = AnyGritMaybeNamedArg (',' AnyGritMaybeNamedArg)* ','?

GritNodeLike =
    name: GritName
    '('
    named_args: GritNamedArgList
    ')'

GritLike =
    'like'
    threshold: GritLikeThreshold?
    '{'
    example: AnyGritPattern
    '}'

GritLikeThreshold =
    '('
    threshold: AnyGritPattern
    ')'

// { key: `pattern` }
// ^ ^^^^ ^^^^^^^^^ ^
GritMap =
    '{'
    elements: GritMapElementList
    '}'

GritMapElementList = GritMapElement (',' GritMapElement)* ','?

// { key: `pattern` }
//   ^^^^ ^^^^^^^^^
GritMapElement =
    key: GritName
    ':'
    value: AnyGritPattern

// $map.value
// ^^^^^^^^^^
GritMapAccessor =
    map: AnyGritMapAccessorSubject
    '.'
    key: AnyGritMapKey

AnyGritMapAccessorSubject = GritMap | AnyGritContainer
AnyGritMapKey = GritName | GritVariable

// list [ `one`, 2, ..., $many ]
// ^^^^ ^ ^^^^^^ ^^ ^^^^ ^^^^^ ^
GritList =
    name: GritName?
    '['
    patterns: GritListPatternList
    ']'

GritListPatternList = AnyGritListPattern (',' AnyGritListPattern)* ','?

// list [ `one`, 2, ..., $many ]
//        ^^^^^  ^  ^^^  ^^^^^
AnyGritListPattern = AnyGritPattern | GritDotdotdot

// list [ `one`, 2, ..., $many ]
//                  ^^^
GritDotdotdot =
    dotdotdot: '...'
    pattern: AnyGritMaybeCurlyPattern?

// $list[0]
// ^^^^^^^^
GritListAccessor =
    list: AnyGritListAccessorSubject
    '['
    index: AnyGritListIndex
    ']'

AnyGritListAccessorSubject = GritList | AnyGritContainer
AnyGritListIndex = AnyGritContainer | GritNegativeIntLiteral | GritIntLiteral

// only valid as a pattern
GritDot =
    dot: '.'

GritSome = 'some' pattern: AnyGritMaybeCurlyPattern
GritEvery = 'every' pattern: AnyGritMaybeCurlyPattern

// only valid as a pattern
GritUnderscore =
    token: '$_'

GritRegexPattern =
    regex: AnyGritRegex
    variables: GritRegexPatternVariables?

GritRegexPatternVariables =
    '('
    args: GritPatternArgList
    ')'

GritPatternArgList = GritVariableList

//////////////
/// PREDICATES
//////////////

// `pattern` where { predicates }
//                 ^^^^^^^^^^^^^^ 
AnyGritPredicate =
    GritPredicateNot
    | GritPredicateMaybe
    | GritPredicateAnd
    | GritPredicateOr
    | GritPredicateAny
    | GritPredicateIfElse
    | GritPredicateAssignment
    | GritPredicateAccumulate
    | GritPredicateRewrite
    | GritPredicateGreater
    | GritPredicateLess
    | GritPredicateGreaterEqual
    | GritPredicateLessEqual
    | GritPredicateNotEqual
    | GritPredicateEqual
    | GritPredicateMatch
    | GritPredicateCall
    | GritBracketedPredicate
    | GritBooleanLiteral
    | GritPredicateReturn
    | GritBogusPredicate

GritPredicateList = AnyGritPredicate (',' AnyGritPredicate)* ','?

GritBracketedPredicate = '(' predicate: AnyGritPredicate ')'

GritPredicateNot =
    not: GritNot
    predicate: AnyGritPredicate

GritPredicateMaybe =
    'maybe'
    predicate: AnyGritPredicate

GritPredicateAnd =
    'and'?
    '{'
    predicates: GritPredicateList
    '}'

GritPredicateOr =
    'or'
    '{'
    predicates: GritPredicateList
    '}'

GritPredicateAny =
    'any'
    '{'
    predicates: GritPredicateList
    '}'

GritPredicateIfElse =
    'if'
    '('
    if_predicate: AnyGritPredicate
    ')'
    then_predicate: AnyGritPredicate
    else_clause: GritPredicateElseClause?

GritPredicateElseClause =
    'else'
    else_predicate: AnyGritPredicate

GritPredicateRewrite =
    left: GritVariable
    annotation: GritAnnotation?
    '=>'
    right: AnyGritPattern

GritPredicateAssignment =
    container: AnyGritContainer
    '='
    pattern: AnyGritPattern

GritPredicateAccumulate =
    left: GritVariable
    '+='
    right: AnyGritPattern

GritPredicateGreater =
    left: GritVariable
    '>'
    right: AnyGritPattern

GritPredicateLess =
    left: GritVariable
    '<'
    right: AnyGritPattern

GritPredicateGreaterEqual =
    left: GritVariable
    '>='
    right: AnyGritPattern

GritPredicateLessEqual =
    left: GritVariable
    '<='
    right: AnyGritPattern

GritPredicateNotEqual =
    left: GritVariable
    '!='
    right: AnyGritPattern

GritPredicateEqual =
    left: GritVariable
    '=='
    right: AnyGritPattern

// `log($message)` where $message <: `Hello, world!`
//                       ^^^^^^^^ ^^ ^^^^^^^^^^^^^^^ 
GritPredicateMatch =
    left: AnyGritPredicateMatchSubject
    match: '<:'
    right: AnyGritPattern

AnyGritPredicateMatchSubject = AnyGritContainer | AnyGritLiteral

GritPredicateCall =
    name: GritName
    '('
    named_args: GritNamedArgList
    ')'

GritPredicateReturn =
    'return'
    pattern: AnyGritPattern

///////////////
/// DEFINITIONS
///////////////

AnyGritDefinition =
    GritPatternDefinition
    | GritPredicateDefinition
    | GritFunctionDefinition
    | GritBogusDefinition

GritDefinitionList = AnyGritDefinition ('newline' AnyGritDefinition)* 'newline'?

// function my_function($arg1, $arg2) { ... }
// ^^^^^^^^ ^^^^^^^^^^^^^^^^^^ ^^^^^^ ^ ^^^ ^
GritFunctionDefinition =
    'function'
    name: GritName
    '('
    args: GritVariableList
    ')'
    body: GritCurlyPredicateList

// private pattern my_function($arg1, $arg2) { ... }
// ^^^^^^^ ^^^^^^^ ^^^^^^^^^^^^^^^^^^ ^^^^^^ ^ ^^^ ^
GritPatternDefinition =
    visibility: 'private'?
    'pattern'
    name: GritName
    '('
    args: GritPatternArgList
    ')'
    language: GritLanguageDeclaration?
    body: GritPatternDefinitionBody

GritPatternDefinitionBody =
    '{'
    patterns: GritPatternList
    '}'

// predicate my_function($arg1, $arg2) { ... }
// ^^^^^^^^^ ^^^^^^^^^^^^^^^^^^ ^^^^^^ ^ ^^^ ^
GritPredicateDefinition =
    'predicate'
    name: GritName
    '('
    args: GritPatternArgList
    ')'
    body: GritCurlyPredicateList

GritCurlyPredicateList =
    '{'
    predicates: GritPredicateList
    '}'

//////////////////////////////////
/// TOKENS AND LEXICAL DEFINITIONS
//////////////////////////////////

GritBooleanLiteral =
    value: ('true' | 'false')

GritVariable = value: 'grit_variable'
GritVariableList = GritVariable (',' GritVariable)* ','?

// name for variables, labels
GritName = value: 'grit_name'

// These are target languages
GritLanguageName =
    language_kind: ('js' | 'css' | 'json' | 'grit' | 'html')

GritBacktickSnippetLiteral = value: 'grit_backtick_snippet'

GritRawBacktickSnippetLiteral = value: 'grit_raw_backtick_snippet'

GritLanguageSpecificSnippet =
    language: GritLanguageName
    snippet: 'grit_string'

// a code snippet may be prefixed by a label;
// the label may be prefixed by a module or sort name (separated from the label by a dot)
GritCodeSnippet =
    source: AnyGritCodeSnippetSource

AnyGritCodeSnippetSource =
    GritBacktickSnippetLiteral
    | GritLanguageSpecificSnippet
    | GritRawBacktickSnippetLiteral

GritNot =
    token: ('not' | '!')

GritUndefinedLiteral = token: 'undefined'

GritIntLiteral = value: 'grit_int'
GritNegativeIntLiteral = value: 'grit_negative_int'

GritDoubleLiteral = value: 'grit_double'

GritStringLiteral = value: 'grit_string'

AnyGritRegex = GritRegexLiteral | GritSnippetRegexLiteral

GritRegexLiteral = value: 'grit_regex'

GritSnippetRegexLiteral = value: 'grit_snippet_regex'

GritAnnotation = value: 'grit_annotation'
