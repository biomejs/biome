// Markdown Un-Grammar.
//
// This grammar specifies the structure of Markdown's concrete syntax tree.
// It does not specify parsing rules (ambiguities, precedence, etc are out of scope).
// Tokens are processed -- contextual keywords are recognised, compound operators glued.
//
// Legend:
//
//   //          				-- comment
//   Name =      				-- non-terminal definition
//   'ident'     				-- token (terminal)
//   A B         				-- sequence
//   A | B       				-- alternation
//   A*          				-- zero or more repetition
//   (A (',' A)* ','?)	        -- repetition of node A separated by ',' and allowing a trailing comma
//   (A (',' A)*)	            -- repetition of node A separated by ',' without a trailing comma
//   A?          				-- zero or one repetition
//   (A)         				-- same as A
//   label:A     				-- suggested name for field of AST node

// NOTES
//
// - SyntaxNode, SyntaxToken and SyntaxElement will be stripped from the codegen
// - Bogus nodes are special nodes used to keep track of broken code; they are
//   not part of the grammar but they will appear inside the green tree


///////////////
// BOGUS NODES
///////////////
// SyntaxElement is a generic data structure that is meant to track nodes and tokens
// in cases where we care about both types
//
// As Bogus* node will need to yield both tokens and nodes without discrimination,
// and their children will need to yield nodes and tokens as well.
// For this reason, SyntaxElement = SyntaxElement

SyntaxElement = SyntaxElement

// Needed by the infrastructure
MdBogus = SyntaxElement*

MdDocument =
  bom: 'UNICODE_BOM'?
  value: MdBlockList
  eof: 'EOF'

MdBlockList = AnyMdBlock*

AnyMdBlock =
	AnyMdLeafBlock
	| AnyMdContainerBlock

AnyMdLeafBlock =
	MdThematicBreakBlock
	| MdHeader
	| MdSetextHeader
	| AnyMdCodeBlock
	| MdHtmlBlock
	| MdLinkReferenceDefinition
	| MdLinkBlock
	| MdParagraph
	| MdNewline

AnyMdContainerBlock =
	MdQuote
	| MdBulletListItem
	| MdOrderedListItem



// ## Lorem
// ^^^^^^^^
MdHeader =
	before: MdHashList
	content: MdParagraph?
	after: MdHashList

MdHashList = MdHash*

MdHash = '#'


// setext title
// foo
// ===
// bar
// ---
// The underline indicates heading level: '=' for H1, '-' for H2
// The underline is stored as a setext underline literal token
MdSetextHeader =
	content: MdInlineItemList
	underline: 'md_setext_underline_literal'

// indented code blocks & fenced code blocks
AnyMdCodeBlock =
	MdIndentCodeBlock
	| MdFencedCodeBlock

//     code
// ^^^^^^^^
// The space before "code" is intentional.
// Indentation is tracked in trivia, so we use MdInlineItemList for content.
MdIndentCodeBlock =
	content: MdInlineItemList


// ```shell
//
// ```
// or
// ~~~shell
//
// ~~~
MdFencedCodeBlock =
	l_fence: ('```' | '~~~')
	code_list: MdCodeNameList
	content: MdInlineItemList
	r_fence: ('```' | '~~~')

MdCodeNameList = MdTextual*

// html block - content is stored as raw text (multiple textual tokens)
MdHtmlBlock = content: MdInlineItemList

// Link reference definition per CommonMark §4.7
// [label]: destination "title" or [label]: destination 'title' or [label]: destination (title)
// Labels are case-insensitive and whitespace-normalized
MdLinkReferenceDefinition =
	'['
	label: MdLinkLabel
	']'
	':'
	destination: MdLinkDestination
	title: MdLinkTitle?

// Label for link reference definitions (CommonMark §4.7)
// Up to 999 chars, no unescaped brackets, whitespace-normalized
// Labels can contain multiple tokens (e.g., "foo-bar" is: foo, -, bar)
MdLinkLabel = content: MdInlineItemList

// Destination URL for link reference definitions
// Can be angle-bracketed <url> or bare url (no whitespace)
// Destinations can contain multiple tokens
MdLinkDestination = content: MdInlineItemList

// Optional title for link reference definitions
// Quoted with ", ', or ()
// Titles can contain multiple tokens
MdLinkTitle = content: MdInlineItemList

// Legacy MdLinkBlock retained for backwards compatibility
MdLinkBlock = label: MdTextual
                url: MdTextual
                title: MdTextual?

MdQuote =
	marker: '>'
	content: MdBlockList

MdBulletListItem = MdBulletList
MdOrderedListItem = MdBulletList


MdBulletList = MdBullet*
// - Hey!
// ^^^^^^
// 1. Hey!
// ^^^^^^^
MdBullet =
	bullet: ('-' | '*' | '+' | 'md_ordered_list_marker')
	content: MdBlockList

// Any block paragraph
//
// Another block paragraph
MdParagraph =
	list: MdInlineItemList
	hard_line: MdHardLine?

MdInlineItemList = AnyMdInline*


AnyMdInline =
	MdInlineCode
	| MdInlineEmphasis
	| MdInlineItalic
	| MdInlineLink
	| MdInlineImage
	| MdReferenceLink
	| MdReferenceImage
	| MdAutolink
	| MdInlineHtml
	| MdEntityReference
	| MdHtmlBlock
	| MdHardLine
	| MdSoftBreak
	| MdTextual


// *italic*
// ^^^^^^^^
MdInlineItalic =
	l_fence: ('*' | '_')
	content: MdInlineItemList
	r_fence: ('*' | '_')

// **bold**
// ^^^^^^^^
MdInlineEmphasis =
	l_fence: ('**' | '__')
	content: MdInlineItemList
	r_fence: ('**' | '__')


// `code`
// ^^^^^^
MdInlineCode =
	l_tick: '`'
	content: MdInlineItemList
	r_tick: '`'

// [text](href)
// ^^^^^^^^^^^^
MdInlineLink =
	'['
	text: MdInlineItemList
	']'
	'('
	destination: MdInlineItemList
	title: MdLinkTitle?
	')'

// ![alt](image)
// ^^^^^^^^^^^^^^
MdInlineImage =
	'!'
	'['
	alt: MdInlineItemList
	']'
	'('
	destination: MdInlineItemList
	title: MdLinkTitle?
	')'

// Reference-style link per CommonMark §6.5
// Full reference: [text][label]
// Collapsed reference: [text][]
// Shortcut reference: [text]
MdReferenceLink =
	'['
	text: MdInlineItemList
	']'
	label: MdReferenceLinkLabel?

// Reference-style image per CommonMark §6.6
// Full reference: ![alt][label]
// Collapsed reference: ![alt][]
// Shortcut reference: ![alt]
MdReferenceImage =
	'!'
	'['
	alt: MdInlineItemList
	']'
	label: MdReferenceLinkLabel?

// Label part of a reference link/image
// Either [label] (full) or [] (collapsed)
// Absent for shortcut references
MdReferenceLinkLabel =
	'['
	label: MdInlineItemList
	']'

// <https://example.com> or <user@example.com>
// Autolinks per CommonMark §6.4 (URI) and §6.5 (email)
MdAutolink =
	'<'
	value: MdInlineItemList
	'>'

// Raw inline HTML per CommonMark §6.8
// Includes: open tags, closing tags, comments, processing instructions,
// declarations, and CDATA sections
MdInlineHtml = value: MdInlineItemList

// Entity and numeric character references per CommonMark §6.2
// Named entities: &name; (2-31 alphanumeric chars starting with letter)
// Decimal numeric: &#digits; (1-7 decimal digits)
// Hexadecimal: &#xhex; or &#Xhex; (1-6 hex digits)
MdEntityReference = value: 'md_entity_literal'

// ***
// ---
// ___
// https://spec.commonmark.org/0.31.2/#container-blocks-and-leaf-blocks
MdThematicBreakBlock = value: 'md_thematic_break_literal'

// Explicit newline node for inter-block newlines.
// This preserves NEWLINEs in the CST without creating "newline-only paragraphs".
// Used when a NEWLINE appears between blocks and isn't part of inline content.
MdNewline = value: 'NEWLINE'

MdHardLine = value: 'md_hard_line_literal'
MdSoftBreak = value: 'md_soft_break_literal'
MdTextual = value: 'md_textual_literal'
MdIndent = value: 'md_indent_chunk_literal'
